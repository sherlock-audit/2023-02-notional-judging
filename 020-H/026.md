ck

high

# `_getTimeWeightedPrimaryBalance` can return zero `underlyingValue` for small poolClaims

## Summary

`_getTimeWeightedPrimaryBalance` can return zero `underlyingValue` for small poolClaims

## Vulnerability Detail

The function `_getTimeWeightedPrimaryBalance` can return zero `underlyingValue` for tokens with low decimals such as USDC.

```solidity
    function _getTimeWeightedPrimaryBalance(
        TwoTokenPoolContext memory poolContext,
        StrategyContext memory strategyContext,
        uint256 poolClaim,
        uint256 oraclePrice,
        uint256 spotPrice
    ) internal view returns (uint256 primaryAmount) {
        // Make sure spot price is within oracleDeviationLimit of pairPrice
        strategyContext._checkPriceLimit(oraclePrice, spotPrice);
        
        // Get shares of primary and secondary balances with the provided poolClaim
        uint256 totalSupply = poolContext.poolToken.totalSupply();
        uint256 primaryBalance = poolContext.primaryBalance * poolClaim / totalSupply;
        uint256 secondaryBalance = poolContext.secondaryBalance * poolClaim / totalSupply;

        // Value the secondary balance in terms of the primary token using the oraclePairPrice
        uint256 secondaryAmountInPrimary = secondaryBalance * strategyContext.poolClaimPrecision / oraclePrice;

        // Make sure primaryAmount is reported in primaryPrecision
        uint256 primaryPrecision = 10 ** poolContext.primaryDecimals;
        primaryAmount = (primaryBalance + secondaryAmountInPrimary) * primaryPrecision / strategyContext.poolClaimPrecision;
    }

```

This is because `uint256 primaryPrecision = 10 ** poolContext.primaryDecimals;` will be a much lower value that `strategyContext.poolClaimPrecision` which is `1e18`. Therefore for small amounts the numerator will be smaller than the denominator resulting in a value of zero being returned due to solidity's rounding down behaviour.

`_getTimeWeightedPrimaryBalance` is used by `Curve2TokenPoolUtils::_convertStrategyToUnderlying` and can return a value of zero for small amounts of `poolClaim`.

```solidity

    function _convertStrategyToUnderlying(
        Curve2TokenPoolContext memory poolContext,
        StrategyContext memory strategyContext,
        uint256 strategyTokenAmount,
        uint256 oraclePrice,
        uint256 spotPrice
    ) internal view returns (int256 underlyingValue) {
        
        uint256 poolClaim 
            = strategyContext._convertStrategyTokensToPoolClaim(strategyTokenAmount);

        underlyingValue 
            = poolContext.basePool._getTimeWeightedPrimaryBalance({
                strategyContext: strategyContext,
                poolClaim: poolClaim,
                oraclePrice: oraclePrice, 
                spotPrice: spotPrice
            }).toInt();
    }  
```

This returned value of zero will then be used in `Curve2TokenPoolUtils::_executeSettlement` to calculate `expectedUnderlyingRedeemed`

```solidity
        int256 expectedUnderlyingRedeemed = poolContext._convertStrategyToUnderlying({
            strategyContext: strategyContext,
            strategyTokenAmount: redeemStrategyTokenAmount,
            oraclePrice: oraclePrice,
            spotPrice: spotPrice
        });
```
This value of `expectedUnderlyingRedeemed` is important in determining account solvency:

```solidity
        // A negative surplus here means the account is insolvent
        // (either expectedUnderlyingRedeemed is negative or
        // expectedUnderlyingRedeemed is less than underlyingCashRequiredToSettle).
        // If that's the case, we should just redeem and repay as much as possible (surplus
        // check is ignored because maxUnderlyingSurplus can never be negative).
        // If underlyingCashRequiredToSettle is negative, that means we already have surplus cash
        // on the Notional side, it will just make the surplus larger and potentially
        // cause it to go over maxUnderlyingSurplus.
        int256 surplus = expectedUnderlyingRedeemed -
            underlyingCashRequiredToSettle;
```

## Impact

Break the functionality of determining account solvency

## Code Snippet

https://github.com/sherlock-audit/2023-02-notional/blob/main/leveraged-vaults/contracts/vaults/common/internal/pool/TwoTokenPoolUtils.sol#L67-L88

https://github.com/sherlock-audit/2023-02-notional/blob/main/leveraged-vaults/contracts/vaults/curve/internal/pool/Curve2TokenPoolUtils.sol#L174-L191

https://github.com/sherlock-audit/2023-02-notional/blob/main/leveraged-vaults/contracts/vaults/common/internal/settlement/SettlementUtils.sol#L119-L128

## Tool used

Manual Review

## Recommendation

Add a check to check whether `underlyingValue == 0` in `Curve2TokenPoolUtils::_convertStrategyToUnderlying` and revert if true.